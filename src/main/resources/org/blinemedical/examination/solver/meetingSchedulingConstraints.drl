package org.blinemedical.examination.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.hardmediumsoft.HardMediumSoftScoreHolder;

import org.blinemedical.examination.domain.AssignedScenarioLearner;
import org.blinemedical.examination.domain.Attendance;
import org.blinemedical.examination.domain.Meeting;
import org.blinemedical.examination.domain.MeetingAssignment;
import org.blinemedical.examination.domain.MeetingSchedule;
import org.blinemedical.examination.domain.Person;
import org.blinemedical.examination.domain.Room;
import org.blinemedical.examination.domain.Scenario;
import org.blinemedical.examination.domain.TimeGrain;

global HardMediumSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "Room conflict"
    when
        $leftAssignment : MeetingAssignment(room != null, $leftId : id, $room : room)
        $rightAssignment : MeetingAssignment(room == $room, calculateOverlap($leftAssignment) > 0, id > $leftId)
    then
        scoreHolder.penalize(kcontext, $rightAssignment.calculateOverlap($leftAssignment));
end

// TODO Might be more efficient if a MeetingAssignment knows timeGrainList.size()
// TODO Can be obsolete if MeetingAssignment uses @ValueRangeProvider from entity for timeGrainRange
rule "Don't go in overtime"
    when
        MeetingAssignment(startingTimeGrain != null, $lastTimeGrainIndex : getLastTimeGrainIndex())
        not TimeGrain(grainIndex == $lastTimeGrainIndex)
    then
        scoreHolder.penalize(kcontext, $lastTimeGrainIndex);
end

rule "Required attendance conflict"
    when
        Attendance($leftId : id, $person : person, $leftMeeting : meeting)
        Attendance(person == $person, $rightMeeting : meeting, id > $leftId)
        $leftAssignment : MeetingAssignment(meeting == $leftMeeting, startingTimeGrain != null)
        $rightAssignment : MeetingAssignment(meeting == $rightMeeting, calculateOverlap($leftAssignment) > 0)
    then
        scoreHolder.penalize(kcontext, $rightAssignment.calculateOverlap($leftAssignment));
end

// ############################################################################
// Medium constraints
// ############################################################################

rule "Scenario scheduled"
    when
        $scenario : Scenario($scenarioId : id)
        MeetingAssignment(
            startingTimeGrain != null,
            room != null,
            meeting.scenarioId == $scenarioId,
            $meeting : meeting,
            $learner : meeting.requiredLearner.person)
    then
        insertLogical(new AssignedScenarioLearner($scenario, $learner));
end

rule "Assigned meetings"
    when
        $scenario : Scenario()
        $learner : Person(!patient)
        exists AssignedScenarioLearner(
            $scenario == scenario,
            $learner == learner)
    then
        scoreHolder.reward(kcontext);
end

rule "Half assigned meetings"
    when
        MeetingAssignment(
            startingTimeGrain == null,
            room != null)
    then
        scoreHolder.penalize(kcontext);
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "Do all meetings as soon as possible"
    when
        MeetingAssignment(startingTimeGrain != null, $lastTimeGrainIndex : lastTimeGrainIndex)
    then
        scoreHolder.penalize(kcontext, $lastTimeGrainIndex);
end

rule "Overlapping meetings"
    when
        $leftAssignment : MeetingAssignment(startingTimeGrain != null, room != null, $leftRoom : room)
        $rightAssignment : MeetingAssignment(startingTimeGrain != null, room == $leftRoom,
            meeting.getId() < $leftAssignment.getMeeting().getId(),
            meeting != $leftAssignment.getMeeting())
    then
        if ($leftAssignment.calculateOverlap($rightAssignment) != 0) {
            scoreHolder.penalize(kcontext);
        }
end

rule "Room stability"
    when
        Attendance($person : person, $leftMeeting : meeting)
        Attendance(person == $person, meeting != $leftMeeting, $rightMeeting : meeting)
        MeetingAssignment(meeting == $leftMeeting, startingTimeGrain != null, $leftStartTimeGrain : startingTimeGrain, $leftRoom : room)
        MeetingAssignment(meeting == $rightMeeting, startingTimeGrain != null, $leftStartTimeGrain.getGrainIndex() < startingTimeGrain.getGrainIndex(),
            $leftRoom != room, startingTimeGrain.getGrainIndex() - $leftMeeting.getDurationInGrains() - $leftStartTimeGrain.getGrainIndex() <= 2)
    then
        scoreHolder.penalize(kcontext);
end
